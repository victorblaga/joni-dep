plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'joni.dep'
version = '0.1.0.BETA'
sourceCompatibility = 1.8
targetCompatibility = 1.8

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'joni-dep'
        name = 'joni-dep'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/victorblaga/joni-dep.git'
        publications = ['Bintray']
        version {
            name = project.version
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    api 'com.typesafe:config:1.3.1'
    implementation 'com.google.guava:guava:23.0'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "victorblaga"
            name "Victor Blaga"
            email "vic.blaga@gmail.com"
        }
    }

    scm {
        url "https://github.com/victorblaga/joni-dep"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Bintray(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'joni-dep'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'joni-dep is a simple dependency injection library for Java')
                root.appendNode('name', 'joni-dep')
                root.appendNode('url', 'https://github.com/victorblaga/joni-dep')
                root.children().last() + pomConfig
            }
        }
    }
}